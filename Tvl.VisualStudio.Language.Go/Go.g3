grammar Go;

options
{
	language=CSharp3;
	output=AST;
	ASTLabelType=CommonTree;
	//backtrack=true;
}

tokens
{
	KW_BREAK       = 'break';
	KW_CASE        = 'case';
	KW_CHAN        = 'chan';
	KW_CONST       = 'const';
	KW_CONTINUE    = 'continue';
	KW_DEFAULT     = 'default';
	KW_DEFER       = 'defer';
	KW_ELSE        = 'else';
	KW_FALLTHROUGH = 'fallthrough';
	KW_FOR         = 'for';
	KW_FUNC        = 'func';
	KW_GO          = 'go';
	KW_GOTO        = 'goto';
	KW_IF          = 'if';
	KW_IMPORT      = 'import';
	KW_INTERFACE   = 'interface';
	KW_MAP         = 'map';
	KW_PACKAGE     = 'package';
	KW_RANGE       = 'range';
	KW_RETURN      = 'return';
	KW_SELECT      = 'select';
	KW_STRUCT      = 'struct';
	KW_SWITCH      = 'switch';
	KW_TYPE        = 'type';
	KW_VAR         = 'var';

	// delimiters
	LPAREN='(';
	RPAREN=')';
	LBRACK='[';
	RBRACK=']';
	LBRACE='{';
	RBRACE='}';
	SEMI=';';
	COMMA=',';
	DOT='.';
	COLON=':';

	// operators
	EQ			='=';
	NEQ			='!=';
	EQEQ		='==';
	PLUS		='+';
	PLUSEQ		='+=';
	MINUS		='-';
	MINUSEQ		='-=';
	TIMES		='*';
	TIMESEQ		='*=';
	DIV			='/';
	DIVEQ		='/=';
	LT			='<';
	GT			='>';
	LE			='<=';
	GE			='>=';
	NOT			='!';
	AND			='&&';
	BITAND		='&';
	ANDEQ		='&=';
	BITCLR		='&^';
	BITCLREQ	='&^=';
	OR			='||';
	BITOR		='|';
	OREQ		='|=';
	INC			='++';
	DEC			='--';
	XOR			='^';
	XOREQ		='^=';
	MOD			='%';
	MODEQ		='%=';
	LSHIFT		='<<';
	RSHIFT		='>>';
	LSHIFTEQ	='<<=';
	RSHIFTEQ	='>>=';
	DEFEQ		=':=';
	ELLIP		='...';
	CHAN		='<-';

	AST_VAR;
	AST_VARS;
	AST_VALUES;
	AST_SENDCHAN;
	AST_RECVCHAN;
	AST_CONST;
	AST_TYPE;

	AST_SELECTOR;
	AST_INDEX_OR_SLICE;
	AST_TYPE_ASSERTION;
	AST_CALL;
	AST_SLICE;
	AST_INDEX;
	AST_METHOD_EXPRESSION;
	AST_COMPOSITE_LITERAL;

	AST_POSITIVE;
	AST_NEGATE;
	AST_COMPLEMENT;
	AST_ADDRESSOF;
	AST_DEREFERENCE;
	AST_RECEIVE;

	UNPARSED_BLOCK;

	FIELD_DECLARATION;
	ANONYMOUS_FIELD_DECLARATION;
	METHOD_RECEIVER;

	FIELD_IDENTIFIER;
	TYPE_IDENTIFIER;
	FUNCTION_IDENTIFIER;
	METHOD_IDENTIFIER;
}

@lexer::namespace{Tvl.VisualStudio.Language.Go}
@parser::namespace{Tvl.VisualStudio.Language.Go}

/*
 * Types
 */

type
	:	typeName
	|	typeLit
	|	'('^ type ')'
	;

typeNoParens
	:	typeName
	|	typeLit
	;

typeName
	:	qualifiedIdent
	;

typeLit
	:	arrayType
	|	structType
	|	pointerType
	|	functionType
	|	interfaceType
	|	sliceType
	|	mapType
	|	channelType
	;

// Array types

arrayType
	:	'['^ arrayLength ']'! elementType
	;

arrayLength
	:	expression
	;

elementType
	:	type
	;

// Slice types

sliceType
	:	'['^ ']' elementType
	;

// Struct types

structType
	:	'struct'^
		'{'!
		(	'}'!
		|	fieldDecl (';'! fieldDecl)* ';'!? '}'!
		)
	;

fieldDecl
	:	(	fieldIdentifierList type tag?
			-> ^(FIELD_DECLARATION[$start] ^(AST_VARS fieldIdentifierList) type tag?)
		|	anonymousField tag?
			-> ^(ANONYMOUS_FIELD_DECLARATION[$start] anonymousField tag?)
		)
	;

fieldIdentifierList
	:	IDENTIFIER<FIELD_IDENTIFIER> (','! IDENTIFIER<FIELD_IDENTIFIER>)*
	;

anonymousField
	:	'*'? typeName
	;

tag
	:	STRING_LITERAL
	;

// Pointer types

pointerType
	:	'*'^ baseType
	;

baseType
	:	type
	;

// Function types

functionType
	:	'func'^ signature
	;

signature
	//:	parameters ((result) => result)?
	:	parameters
		(	('(') => parameters
		|	typeNoParens?
		)
	;

//result
//	:	parameters
//	|	typeNoParens
//	;

parameters
	:	'('^ (parameterList ','!?)? ')'
	;

parameterList
	:	parameterDecl (','! parameterDecl)*
	;

parameterDecl
	:	(IDENTIFIER ~('.'|','|')')) =>
		// using IDENTIFIER instead of identifierList because parameterDecl is only used by the parameterList rule, which is itself comma separated
		IDENTIFIER^ parameterType
	|	parameterType
	;

parameterType
	:	'...'^? type
	;

// Interface types

interfaceType
	:	'interface'^
		'{'!
		(	'}'!
		|	methodSpec (';'! methodSpec)* ';'!? '}'!
		)
	;

methodSpec
	:	methodName^ signature
	|	interfaceTypeName
	;

methodName
	:	IDENTIFIER<METHOD_IDENTIFIER>
	;

interfaceTypeName
	:	typeName
	;

// Map types

mapType
	:	'map'^ '['! keyType ']'! elementType
	;

keyType
	:	type
	;

// Channel types

channelType
	:	('<-' 'chan') => recvChannel
	|	('chan' '<-') => sendChannel
	|	channel
	;

channel
	:	'chan'^ elementType
	;

sendChannel
	:	'chan'<AST_SENDCHAN>^ '<-'! elementType
	;

recvChannel
	:	'<-'! 'chan'<AST_RECVCHAN>^ elementType
	;

/*
 * Blocks
 */

block
	:	'{'<UNPARSED_BLOCK>^
			(	~('{'|'}')!
			|	block!
			)*
		'}'!
	;

statementBlock
	:	'{'^
		statement (';'! statement)*
		'}'!
	;

/*
 * Declarations and scope
 */

declaration
	:	constDecl
	|	typeDecl
	|	varDecl
	;

topLevelDecl
	:	declaration
	|	functionDecl
	|	methodDecl
	;

// Constant declarations

constDecl
	:	'const'^
		(	constSpec
		|	'('! (constSpec ';'!)* ')'!
		)
	;

constSpec
	:	identifierList
		(type? '=' expressionList)?
		-> ^(AST_CONST ^(AST_VARS identifierList?) ^(AST_TYPE type?) ^(AST_VALUES expressionList?))
	;

identifierList
	:	IDENTIFIER (','! IDENTIFIER)*
	;

expressionList
	:	expression (','! expression)*
	;

// Type declarations

typeDecl
	:	'type'^
		(	typeSpec
		|	'('! (typeSpec ';'!)* ')'!
		)
	;

typeSpec
	:	IDENTIFIER<TYPE_IDENTIFIER>^ type
	;

// Variable declarations

varDecl
	:	'var'^
		(	varSpec
		|	'('! (varSpec ';'!)* ')'!
		)
	;

varSpec
	:	vars=identifierList
		(	type ('=' values=expressionList)?
		|	'=' values=expressionList
		)
		-> ^(AST_VAR ^(AST_VARS $vars) ^(AST_TYPE type?) ^(AST_VALUES $values?))
	;

// Short variable declarations

shortVarDecl
	:	identifierList ':=' expressionList
		-> ^(':=' ^(AST_VARS identifierList) ^(AST_VALUES expressionList))
	;

// Function declarations

functionDecl
	:	'func'^ IDENTIFIER<FUNCTION_IDENTIFIER> signature body?
	;

body
	:	block
	;

// Method declarations

methodDecl
	:	'func'^ receiver methodName signature body?
	;

receiver
	:	'('<METHOD_RECEIVER>^ IDENTIFIER? '*'? baseTypeName ')'
	;

baseTypeName
	:	IDENTIFIER
	;

/*
 * Expressions
 */

// Operands

operand
options {backtrack=true;}
	:	literal
	|	methodExpr
	|	qualifiedIdent
	|	'(' expression ')'
	;

literal
	:	basicLit
	|	compositeLit
	|	functionLit
	;

basicLit
	:	NUMBER
	|	CHAR_LITERAL
	|	STRING_LITERAL
	;

// Qualified identifiers

qualifiedIdent
	:	IDENTIFIER (('.' IDENTIFIER) => '.'^ IDENTIFIER)?
		// if the '.' is present, the first identifier is the package name
	;

// Composite literals

compositeLit
	:	literalType '{'<AST_COMPOSITE_LITERAL>^ (elementList ','?)? '}'
	;

literalType
	:	structType
	|	arrayType
	|	'['^ '...' ']' elementType
	|	sliceType
	|	mapType
	|	typeName
	|	'('^ literalType ')'
	;

elementList
	:	element (',' element)*
	;

element
	:	((key ':') => key ':')? value
	;

key
	:	// fieldName |
		elementIndex
	;

fieldName
	:	IDENTIFIER
	;

elementIndex
	:	expression
	;

value
	:	expression
	;

// Function literals

functionLit
	:	functionType body
	;

// Primary expressions

primaryExpr
	:	(	operand
			-> operand
		//|	conversion
		//|	builtinCall
		)
		(	selector
			-> ^(AST_SELECTOR $primaryExpr selector)
		|	indexOrSlice
			-> ^(AST_INDEX_OR_SLICE $primaryExpr indexOrSlice)
		//|	index
		//|	slice
		|	typeAssertion
			-> ^(AST_TYPE_ASSERTION $primaryExpr typeAssertion)
		|	call
			-> ^(AST_CALL $primaryExpr call)
		)*
	;

selector
	:	'.'^ IDENTIFIER
	;

indexOrSlice
	:	lb='['
		left=expression
		(	// slice
			':' right=expression? ']'
			-> ^(AST_SLICE[$lb] $left ':' $right? ']')
		|	// index
			']'
			-> ^(AST_INDEX[$lb] $left ']')
		)
	;

//index
//	:	'['^ expression ']'
//	;

//slice
//	:	'['^ expression ':' expression? ']'
//	;

typeAssertion
	:	'.'^ '(' type ')'
	;

call
	:	'('^
		(	options {backtrack=true;}
		:	expressionList ','?
		|	type (',' expressionList)? ','?
		)?
		')'
	;

// Selectors
// Indexes
// Slices
// Type assertions
// Calls
// Passing arguments to ... parameters
// Operators

//expressionTest
//	:	expressionTest ',' expressionTest
//	|	expressionTest '||' expressionTest
//	|	expressionTest '&&' expressionTest
//	|	expressionTest '<-' expressionTest
//	|	expressionTest ('==' | '!=' | '<' | '<=' | '>' | '>=') expressionTest
//	|	expressionTest ('+' | '-' | '|' | '^') expressionTest
//	|	expressionTest ('*' | '/' | '%' | '<<' | '>>' | '&' | '&^') expressionTest
//	|	'(' expressionTest ')'
//	|	expressionTest '(' expressionTest ')'
//	|	expressionTest '[' expressionTest ']'
//	;

expression
	:	unaryExpr ((binaryOp unaryExpr) => binaryOp^ unaryExpr)*
	;

unaryExpr
	:	unaryOp^ unaryExpr
	|	primaryExpr
	;

binaryOp
	:	logOp
	|	comOp
	|	relOp
	|	addOp
	|	mulOp
	;

logOp
	:	'||' | '&&'
	;

comOp
	:	'<-'
	;

relOp
	:	'==' | '!=' | '<' | '<=' | '>' | '>='
	;

addOp
	:	'+' | '-' | '|' | '^'
	;

mulOp
	:	'*' | '/' | '%' | '<<' | '>>' | '&' | '&^'
	;

unaryOp
	:	'+'<AST_POSITIVE> | '-'<AST_NEGATE> | '!' | '^'<AST_COMPLEMENT> | '*'<AST_DEREFERENCE> | '&'<AST_ADDRESSOF> | '<-'<AST_RECEIVE>
	;

// Operator precedence
// Arithmetic operators
// Integer overflow
// Comparison operators
// Logical operators
// Address operators
// Communication operators
// Method expressions

methodExpr
	:	receiverType '.'<AST_METHOD_EXPRESSION>^ methodName
	;

receiverType
	:	typeName
	|	'(' '*' typeName ')'
	;

// Conversions

conversion
	:	literalType '(' expression ')'
	;

// Constant expressions
// Order of evaluation

/*
 * Statements
 */

statement
	:	declaration
	|	labeledStmt
	|	simpleStmt
	|	goStmt
	|	returnStmt
	|	breakStmt
	|	continueStmt
	|	gotoStmt
	|	fallthroughStmt
	|	block
	|	ifStmt
	|	switchStmt
	|	selectStmt
	|	forStmt
	|	deferStmt
	;

simpleStmt
	:	lhs=expression
		(	(',' lhs2=expressionList)?
			(	//shortVarDecl
				':=' rhs=expressionList
				-> ^(':=' ^(AST_VARS $lhs $lhs2?) ^(AST_VALUES $rhs))
			|	// assignment
				assignOp rhs=expressionList
				-> ^(assignOp ^(AST_VARS $lhs $lhs2?) ^(AST_VALUES $rhs))
			)
		|	// incDecStmt
			(	'++'
				-> ^('++' $lhs)
			|	'--'
				-> ^('--' $lhs)
			)
		|	//expression statement
			-> $lhs
		)
	|	emptyStmt
	;

//simpleStmt
//options{backtrack=true;}
//	:	shortVarDecl
//	|	assignment
//	|	incDecStmt
//	|	expressionStmt
//	|	emptyStmt
//	;

// Empty statements

emptyStmt
	:
	;

// Labeled statements

labeledStmt
	:	label ':' statement
	;

label
	:	IDENTIFIER
	;

// Expression statements

expressionStmt
	:	expression
	;

// IncDec statements

incDecStmt
	:	expression ('++' | '--')
	;

// Assignments

assignment
	:	expressionList assignOp expressionList
	;

assignOp
	:	'='
	|	'+=' | '-=' | '|=' | '^='
	|	'*=' | '/=' | '%=' | '<<=' | '>>=' | '&=' | '&^='
	;

// If statements

ifStmt
	:	'if'^ ((simpleStmt ';') => simpleStmt ';')? expression? block ('else' statement)?
	;

// Switch statements

switchStmt
options {backtrack=true;}
	:	exprSwitchStmt
	|	typeSwitchStmt
	;

exprSwitchStmt
	:	'switch'^ ((simpleStmt ';') => simpleStmt ';')? expression? '{' exprCaseClause* '}'
	;

exprCaseClause
	:	exprSwitchCase ':' (statement ';')*
	;

exprSwitchCase
	:	'case'^ expressionList
	|	'default'
	;

typeSwitchStmt
	:	'switch'^ ((simpleStmt ';') => simpleStmt ';')? typeSwitchGuard '{' typeCaseClause* '}'
	;

typeSwitchGuard
	:	(IDENTIFIER ':=')? primaryExpr '.' '(' 'type' ')'
	;

typeCaseClause
	:	typeSwitchCase ':' (statement ';')*
	;

typeSwitchCase
	:	'case'^ typeList
	|	'default'
	;

typeList
	:	type (','! type)*
	;

// For statements

forStmt
	:	'for'^
		(	options{backtrack=true;}
		:	rangeClause
		|	forClause
		|	condition
		)?
		block
	;

condition
	:	expression
	;

forClause
	:	// initStmt and postStmt are not optional because one of the simpleStmt is the emptyStmt
		initStmt ';' condition? ';' postStmt
	;

initStmt
	:	simpleStmt
	;

postStmt
	:	simpleStmt
	;

rangeClause
	:	expressionList ('=' | ':=') 'range' expression
	;

// Go statements

goStmt
	:	'go'^ expression
	;

// Select statements

selectStmt
	:	'select'^ '{' commClause* '}'
	;

commClause
	:	commCase ':' (statement ';')*
	;

commCase
	:	'case'^
		(	(~'<-') => expression
			(	// recvExpr
				('=' | ':=') '<-' expression
			|	// sendExpr
				'<-' expression
			)
		|	// recvExpr
			'<-' expression
		)
	|	'default'
	;

//sendExpr
//	:	expression '<-' expression
//	;

//recvExpr
//	:	(expression ('=' | ':='))? '<-' expression
//	;

// Return statements

returnStmt
	:	'return'^ expressionList?
	;

// Break statements

breakStmt
	:	'break'^ label?
	;

// Continue statements

continueStmt
	:	'continue'^ label?
	;

// Goto statements

gotoStmt
	:	'goto'^ label
	;

// Fallthrough statements

fallthroughStmt
	:	'fallthrough'
	;

// Defer statements

deferStmt
	:	'defer'^ expression
	;

/*
 * Built-in functions
 */

//builtinCall
//	:	IDENTIFIER '(' builtinArgs? ')'
//	;

//builtinArgs
//	:	type (',' expressionList)?
//	|	expressionList
//	;

/*
 * Packages
 */

// Source file organization

public
compilationUnit
	:	packageClause ';'!
		(importDecl ';'!)*
		(topLevelDecl ';'!)*
		EOF
	;

// Package clause

packageClause
	:	'package'^ packageName
	;

packageName
	:	IDENTIFIER
	;

// Import declarations

importDecl
	:	'import'
		(	importSpec
		|	'(' (importSpec ';')* ')'
		)
		-> ^('import' importSpec*)
	;

importSpec
	:	('.' | packageName)?
		STRING_LITERAL^
	;

/*//////////////////////////////////////////////////////////////////////////*/
//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////
// L E X E R

IDENTIFIER
	:	('a'..'z' | 'A'..'Z' | '_')
		('a'..'z' | 'A'..'Z' | '0'..'9' | '_')*
	;

NUMBER
	:	(	'0'..'9'
		| 	'.' '0'..'9'
		)+
		(IDENTIFIER NUMBER?)*
		'.'*
	;

COMMENT
	:	'//' (~('\r' | '\n'))*
		{Skip();}
	;

ML_COMMENT
	:	'/*' .* '*/'
		{Skip();}
	;

CHAR_LITERAL
	:	'\''
		(	'\\' .
		|	~('\r' | '\n' | '\'' | '\\')
		)*
		'\''
	;

STRING_LITERAL
	:	INTERP_STRING_LITERAL
	|	RAW_STRING_LITERAL
	;

fragment
INTERP_STRING_LITERAL
	:	'"'
		(	'\\' .
		|	~('\r' | '\n' | '"' | '\\')
		)*
		'"'
	;

fragment
RAW_STRING_LITERAL
	:	'`' .* '`'
	;

WS
	:	(' ' | '\t')+
		{Skip();}
	;

NEW_LINE
	:	(	'\r' '\n'?
		|	'\n'
		)
		{$channel = Hidden;}
	;

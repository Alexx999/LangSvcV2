lexer grammar InsideClassifierLexer;

options {
	language=CSharp3;
	TokenLabelType=CommonToken;
	tokenVocab=OutsideClassifierLexer;
}

@namespace{Tvl.VisualStudio.Language.StringTemplate4}
@ctorModifier{private}

DOT : '.';
ELLIPSIS : '...';
COMMA : ',';
COLON : ':';
SEMI : ';';
LPAREN : '(';
RPAREN : ')';
LBRACK : '[';
RBRACK : ']';
EQUALS : '=';
NOT : '!';
OR : '||';
AND : '&&';
LBRACE : '{';
RBRACE : '}';

REGION_REF
	:	'@' EXPR_IDENTIFIER
	;

EXPR_IDENTIFIER
	:	('a'..'z' | 'A'..'Z' | '_')
		('a'..'z' | 'A'..'Z' | '0'..'9' | '_' | '/')*
	;

WS
	:	(' '|'\t')+
	;

NEWLINE
	:	('\r'|'\n')+
	;

STRING
	:	'"'
		(	~('"'|'\\')
		|	'\\' .
		)*
		'"'
	;

//LDELIM	: {input.LA(1) == OpenDelimiter}? => .;
RDELIM	: {input.LA(1) == CloseDelimiter}? => .;

ANYCHAR
	:	.
	;

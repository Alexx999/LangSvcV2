grammar Alloy;

options {
    language=CSharp3;
    TokenLabelType=IToken;
    output=AST;
    ASTLabelType=CommonTree;
}

tokens {
	LPAREN		= '(';
	RPAREN		= ')';
	LBRACE		= '{';
	RBRACE		= '}';
	LBRACK		= '[';
	RBRACK		= ']';
	LT			= '<';
	GT			= '>';
	LE			= '=<';
	GE			= '>=';
	SLASH		= '/';
	COLON		= ':';
	COMMA		= ',';
	NOT			= '!';
	COUNT		= '#';
	AND			= '&&';
	BITAND		= '&';
	STAR		= '*';
	PLUS		= '+';
	MINUS		= '-';
	OVERRIDE	= '++';
	ARROW		= '->';
	DOT			= '.';
	LSHIFT		= '<<';
	RSHIFT		= '>>';
	URSHIFT		= '>>>';
	IFF			= '<=>';
	DOMAIN_RES	= '<:';
	RANGE_RES	= ':>';
	EQ			= '=';
	IMPLIES		= '=>';
	AT			= '@';
	CARET		= '^';
	BAR			= '|';
	OR			= '||';
	TILDE		= '~';

	KW_ALL		= 'all';
	KW_AS		= 'as';
	KW_BUT		= 'but';
	KW_DISJ		= 'disj';
	KW_ELSE		= 'else';
	KW_EXPECT	= 'expect';
	KW_EXTENDS	= 'extends';
	KW_FOR		= 'for';
	KW_IDEN		= 'iden';
	KW_IMPLIES	= 'implies';
	KW_IN		= 'in';
	KW_LET		= 'let';
	KW_NO		= 'no';
	KW_NONE		= 'none';
	KW_SEQ		= 'seq';
	KW_SEQINT	= 'seq/Int';
	KW_SET		= 'set';
	KW_SUM		= 'sum';
	KW_THIS		= 'this';
	KW_UNIV		= 'univ';
	KW_INT		= 'int';
	KW_INT2		= 'Int';
	KW_NOT		= 'not';
	KW_OR		= 'or';
	KW_AND		= 'and';
	KW_IFF		= 'iff';
	KW_OPEN		= 'open';
	KW_SIG		= 'sig';
	KW_FUN		= 'fun';
	KW_PRED		= 'pred';
	KW_ASSERT	= 'assert';
	KW_FACT		= 'fact';
	KW_RUN		= 'run';
	KW_CHECK	= 'check';
	KW_ENUM		= 'enum';
	KW_MODULE	= 'module';
	KW_ABSTRACT	= 'abstract';
	KW_ONE		= 'one';
	KW_SOME		= 'some';
	KW_LONE		= 'lone';
	KW_PRIVATE	= 'private';
	KW_EXACTLY	= 'exactly';
}

@lexer::namespace{Tvl.VisualStudio.Language.Alloy}
@parser::namespace{Tvl.VisualStudio.Language.Alloy}

/*
 * Parser Rules
 */

public
compilationUnit
	:	specification EOF
	;

specification
	:	module? open* paragraph*
	;

module
	:	'module'^ name ('[' 'exactly'? name (',' 'exactly'? number)* ']')?
	;

open
	:	'private'? 'open'^ name ('[' ref_ (',' ref_)* ','? ']')? ('as' name)?
	;

paragraph
	:	factDecl | assertDecl | funDecl | cmdDecl | enumDecl | sigDecl
	;

factDecl
	:	'fact'^ name? block
	;

assertDecl
	:	'assert'^ name? block
	;

funDecl
	:	'private'?
		(	'fun'^ functionName functionParameters? functionReturn block
		|	'pred'^ functionName functionParameters? block
		)
	;

functionName
	:	(ref_ '.'^)? name
	;

functionReturn
	:	':'^ expr
	;

functionParameters
	:	'('^ declList? ')'
	|	'['^ declList? ']'
	;

declList
	:	decl (','! decl)* ','!?
	;

cmdDecl
	:	(name ':')? ('run'|'check')^ (name|block)? cmdScope
	;

cmdScope
	:	cmdScopeFor? cmdScopeExpect?
	;

cmdScopeFor
	:	'for'^
		(	number 'but' typescopeDeclList
		|	typescopeDeclList
		)
	;

cmdScopeExpect
	:	'expect'^ number?
	;

typescopeDeclList
	:	typescope (','! typescope)* ','!?
	;

typescope
	:	'exactly'? number^ (name|'int'|'seq')?
	;

sigDecl
	:	sigQual* 'sig'^ nameDeclList sigExt? sigBody block?
	;

nameList
	:	name (','^ name (','! name)*)?
	;

nameDeclList
	:	nameList ','!?
	;

sigBody
	:	'{'^ (decl (','! decl)* ','!?)? '}'
	;

enumDecl
	:	'enum'^ name enumBody
	;

enumBody
	:	'{'^ nameList '}'
	;

sigQual
	:	'abstract' | 'lone' | 'one' | 'some' | 'private'
	;

sigExt
	:	'extends'^ ref_
	|	'in'^ ref_ ('+'! ref_)*
	;

public
expr
	:	unOpExpr1
	;

//expr2
//	:	primaryExpr2
//	|	('~'|'*'|'^')^ expr2
//	|	expr2 '.'^ expr2
//	|	expr2 '['^ (expr2 (','! expr2)*)? ']'
//	|	expr2 ':>'^ expr2
//	|	expr2 '<:'^ expr2
//	|	expr2 arrowMultiplicity? '->'^ expr2
//	|	expr2 '&'^ expr2
//	|	expr2 '++'^ expr2
//	|	'#'^ expr2
//	|	('+'|'-')^ expr2
//	|	expr2 ('<<'|'>>'|'>>>')^ expr2
//	|	expr2 ('!'|'not')? ('<'|'>'|'=<'|'>='|'='|'in')^ expr2
//	|	('!'|'not')^ expr2
//	|	expr2 ('&&'|'and')^ expr2
//	|	expr2 ('=>'|'implies')^ expr2 (('else'|',') => ('else'|',') expr2)?
//	|	expr2 ('<=>'|'iff')^ expr2
//	|	expr2 ('||'|'or')^ expr2
//	|	'let'^ letDecl2 (','! letDecl2)* ','? (block2 | '|' expr2)
//	|	quant^ decl2 (','! decl2)* ','!? (block2 | '|' expr2)
//	|	('no'|'some'|'lone'|'one'|'set'|'seq')^ expr2
//	;

//primaryExpr2
//	:	'none'
//	|	'iden'
//	|	'univ'
//	|	'Int'
//	|	'seq/Int'
//	|	'('^ expr ')'
//	|	'@'^? name
//	|	number
//	|	block2
//	//|	'{' decl (',' decl)* ','? blockOrBar '}'
//	;

//block2
//	:	'{'^ expr* '}'
//	;

//decl2
//	:	'private'? 'disj'? nameList ':'^ 'disj'? expr
//	;

//letDecl2
//	:	name '='^ expr
//	;

unOpExpr1
	:	('let' | quantDecls ('{' | '|')) =>
		(	letDecls
		|	quantDecls
		)
		(	block
			-> ^(LBRACE[$block.start] letDecls? quantDecls? block)
		|	'|' expr
			-> ^('|' letDecls? quantDecls? expr)
		)
	|	binOpExpr2
	;

letDecls
	:	'let'^ letDecl (','! letDecl)* ','!?
	;

quantDecls
	:	quant^ decl (','! decl)* ','!?
	;

binOpExpr2
	:	binOpExpr3 (('||'|'or')^ binOpExpr3)*
	;

binOpExpr3
	:	binOpExpr4 (('<=>'|'iff')^ binOpExpr4)*
	;

binOpExpr4
	:	binOpExpr5 (options{k=1; greedy=true;} : ('=>'|'implies')^ binOpExpr5 (options{k=1; greedy=true;} : elseClause)?)*
	;

elseClause
	:	('else'|',')^ binOpExpr5
	;

binOpExpr5
	:	unOpExpr6 (('&&'|'and')^ unOpExpr6)*
	;

unOpExpr6
	:	('!'|'not')^ unOpExpr6
	|	binOpExpr7
	;

binOpExpr7
	:	unOpExpr8 ((('!'|'not')? compareOp^) unOpExpr8)*
	;

compareOp
	:	'<' | '>' | '=<' | '>=' | '=' | 'in'
	;

unOpExpr8
	:	('no'|'some'|'lone'|'one'|'set'|'seq') => ('no'|'some'|'lone'|'one'|'set'|'seq')^ unOpExpr8
	|	binOpExpr9
	;

binOpExpr9
	:	unOpExpr10 (('<<'|'>>'|'>>>')^ unOpExpr10)*
	;

unOpExpr10
	:	/*('+'|'-')^ unOpExpr10
	|	*/countOpExpr11 (('+'|'-')^ countOpExpr11)*
	;

countOpExpr11
	:	'#'^? binOpExpr12
	;

binOpExpr12
	:	binOpExpr13 ('++'^ binOpExpr13)*
	;

binOpExpr13
	:	binOpExpr14 ('&'^ binOpExpr14)*
	;

// let unOpExpr8 handle the right hand side's multiplicity
binOpExpr14
	:	binOpExpr15 (arrowMultiplicity? '->'^ arrowMultiplicity? binOpExpr15)*
	;

binOpExpr15
	:	binOpExpr16 ('<:'^ binOpExpr16)*
	;

binOpExpr16
	:	callExpr17 (':>'^ callExpr17)*
	;

callExpr17
	:	binOpExpr18
	;

binOpExpr18
	:	unOpExpr19
		(	'.'^ unOpExpr19
		|	'['^ (expr (','! expr)*)? ']'
		)*
	;

unOpExpr19
	:	('~'|'*'|'^')^ unOpExpr19
	|	primaryExpr
	;

primaryExpr
	:	'none'
	|	'iden'
	|	'univ'
	|	'Int'
	|	'seq/Int'
	|	'('^ expr ')'
	|	'@'^? name
	|	number
	|	block
	//|	'{' decl (',' decl)* ','? blockOrBar '}'
	;

decl
	:	'private'? 'disj'? nameList ':'^ 'disj'? expr
	;

letDecl
	:	name '='^ expr
	;

quant
	:	'all' | 'no' | 'some' | 'lone' | 'one' | 'sum'
	;

arrowMultiplicity
	:	('some'|'one'|'lone'|'set')
	;

block
	:	'{'^ expr* '}'
	;

//blockOrBar
//	:	block
//	|	'|'^ expr
//	;

name
	:	('this'|IDENTIFIER) ('/'^ IDENTIFIER)*
	;

number
	:	INTEGER
	;

ref_
	:	name | 'univ' | 'Int' | 'seq/Int'
	;

/*
 * Lexer Rules
 */

IDENTIFIER
	:	('a'..'z' | 'A'..'Z' | '_')
		('a'..'z' | 'A'..'Z' | '_' | '0'..'9' | '\'' | '"')*
	;

INTEGER
	:	'0'..'9'+
	;

WS
	:	(' ' | '\t')+
		{Skip();}
	;

NEWLINE
	:	'\r' '\n'?	{Skip();}
	|	'\n'		{Skip();}
	;

COMMENT
	:	('//'|'--') ~('\r'|'\n')*
		{Skip();}
	;

ML_COMMENT
	:	'/*' .* '*/'
		{Skip();}
	;

ANY_CHAR
	:	.
	;
